# Maintainer:  Xiaoxiao Pu <i@xiaoxiao.im>

pkgname=iosocks-git
pkgver=r133.8cad435
pkgrel=1
pkgdesc="A lightweight tunnel proxy"
arch=('i686' 'x86_64')
url="https://github.com/XiaoxiaoPu/iosocks"
license=('GPL3')
depends=('libev')
makedepends=('git')
provides=('iosocks')
source=("git+https://github.com/XiaoxiaoPu/iosocks.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/iosocks"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/iosocks"
	autoreconf -if
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/iosocks"
	make DESTDIR="${pkgdir}/" install
}